import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'application'
    id 'idea'
    id 'com.gorylenko.gradle-git-properties' version '1.4.21'
    id 'com.github.johnrengelman.shadow' version '2.0.3'
}

group = 'space.npstr.ddfb' //discord dialogflow bot
version = '0.0.3-SNAPSHOT'
ext {
    moduleName = 'ddfb'
}

mainClassName = "space.npstr.ddfb.Main"

sourceCompatibility = 10
targetCompatibility = 10

repositories {
    jcenter()                               // JDA, maybe others
    mavenCentral()                          // everything else
    mavenLocal()                            // local maven repo, mostly for testing stuff
    maven { url 'https://jitpack.io' }      // for getting builds from github
}

dependencies {
    compile 'net.dv8tion:JDA:3.6.0_356'                                             //discord API
    compile 'org.slf4j:slf4j-api:1.7.25'                                            //logging framework
    compile 'ch.qos.logback:logback-classic:1.2.3'                                  //logging framework
    compile 'org.yaml:snakeyaml:1.20'                                               //parse yaml files

    compile 'ai.api:libai:1.6.12' //https://github.com/dialogflow/dialogflow-java-client

    //QA
    compile 'com.github.spotbugs:spotbugs-annotations:3.1.2'
}

wrapper {
    gradleVersion = '4.8.1'
    //noinspection UnnecessaryQualifiedReference
    distributionType = Wrapper.DistributionType.ALL
}

compileJava.dependsOn 'clean'
compileJava.options.encoding = 'UTF-8'
compileJava.options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"


shadowJar {
    archiveName = "ddfb.jar"
}

processResources {
    //inject values into app.properties
    filesMatching("**/app.properties") {
        filter ReplaceTokens, tokens: [
                "project.version"   : project.version,
                "project.groupId"   : project.group,
                "project.artifactId": project.ext.moduleName,
                "env.BUILD_NUMBER"  : (System.getenv('CI') ? System.getenv('BUILD_NUMBER') : 'DEV'),
                "env.BUILD_TIME"    : System.currentTimeMillis() + ''
        ]
    }
}
